//! Parser for the LMC.

/// Valid name for a label
labelName = @{ ASCII_ALPHA+ }

/// A labeled instruction
label = { (comment ~ NEWLINE*)* ~ labelName ~ ":" ~ comment? }

/// A generic programmers comment
comment = { ";" ~ (!NEWLINE ~ ANY)* }

/// A memory location, using either physical or labeled
memoryLocation = @{ (ASCII_DIGIT{1, 3} ~ !(ASCII_ALPHANUMERIC+)) | ASCII_ALPHA+ }

/// Valid mnemonic instruction names
instructionName = @{ ASCII_ALPHA{3} ~ !(ASCII_ALPHANUMERIC+) }

/// A instruction
instruction = {
    (comment ~ NEWLINE*)* ~ instructionName ~ memoryLocation? ~ comment?
}

/// Single processable statement
stmt = { (label ~ NEWLINE*)? ~ instruction }

/// A complete program
program = _{ SOI ~ NEWLINE* ~ ((stmt | comment) ~ NEWLINE+)* ~ (stmt | comment)? ~ EOI }

WHITESPACE = _{ " " | "\t" }
